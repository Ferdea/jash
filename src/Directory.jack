class Directory {
    static String tooManyDirectoriesMessage;
    static String tooManyFilesMessage;

    field String name;
    field List directories;
    field List files;
    field Directory parent;

    function void init() {
        let tooManyDirectoriesMessage = "TOO MANY DIRECTORIES";
        let tooManyFilesMessage = "TOO MANY FILES";
        return;
    }

    constructor Directory new(String nameArg, Directory parentArg) {
        if (nameArg.length() = 0) {
            do Sys.error(32);
        }

        let name = nameArg;
        let directories = List.new(4);
        let files = List.new(4);
        let parent = parentArg;
        return this;
    }

    method void dispose() {
        var Directory directory;
        var File file;

        while (directories.getSize() > 0) {
            let directory = directories.remove();
            do directory.dispose();
        }

        while (files.getSize() > 0) {
            let file = files.remove();
            do file.dispose();
        }

        do directories.dispose();
        do files.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method String getName() {
        return name;
    }

    method int getDirectoriesCount() {
        return directories.getSize();
    }

    method Directory directoryAt(int i) {
        return directories.elementAt(i);
    }

    method int directoryByName(String name) {
        var int i, count;
        var Directory directory;
        var bool isFound;

        let isFound = false;
        let count = directories.getSize();
        let i = 0;
        while ((i < count) & (~(isFound))) {
            let directory = directories.elementAt(i);

            if (StringComparer.compare(directory.getName(), name)) {
                let isFound = true;
            }

            let i = i + 1;
        }

        if (~isFound) {
            return -1;
        }
        
        return i - 1;
    }

    method int getFilesCount() {
        return files.getSize();
    }

    method File fileAt(int i) {
        return files.elementAt(i);
    }

    method int fileByName(String name) {
        var int i, count;
        var File file;
        var bool isFound;

        let isFound = false;
        let count = files.getSize();
        let i = 0;
        while ((i < count) & (~(isFound))) {
            let file = files.elementAt(i);

            if (StringComparer.compare(file.getName(), name)) {
                let isFound = true;
            }

            let i = i + 1;
        }

        if (~isFound) {
            return -1;
        }

        return i - 1;
    }

    method void pushDirectory(Directory directory) {
        do directories.add(directory);
        return;
    }

    method void removeDirectory(int i) {
        var Directory directory;
        let directory = directories.removeAt(i);
        do directory.dispose();
        return;
    }

    method void pushFile(File file) {
        do files.add(file);
        return;
    }

    method void removeFile(int i) {
        var File file;
        let file = files.removeAt(i);
        do file.dispose();
        return;
    }

    method bool haveParent() {
        return ~(parent = null);
    }

    method Directory getParent() {
        return parent;
    }

    method String getPath() {
        var StringBuilder pathBuilder;
        var String result;

        let pathBuilder = StringBuilder.new(4);
        do getPathRecursive(pathBuilder);
        let result = pathBuilder.build();
        do pathBuilder.dispose();

        return result;
    }

    method void getPathRecursive(StringBuilder pathBuilder) {
        if (parent = null) {
            do pathBuilder.addString(name);
            return;
        }

        do parent.getPathRecursive(pathBuilder);
        do pathBuilder.add(47);
        do pathBuilder.addString(name);
        return;
    }
}