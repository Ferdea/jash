class Jash {
    static String commandReadMessage;

    field JashState state;

    function void init() {
        let commandReadMessage = " > ";
        return;
    }

    constructor Jash new() {
        let state = JashState.new();
        return this;
    }

    method void dispose() {
        do state.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method void run() {
        var String command;
        var ContentBuffer content;

        while (true) {
            let command = readCommand();
            let content = processCommand(command);
            do command.dispose();

            if (content = null) {
                do JashErrors.incorrectCommand();
            } else {
                do printContent(content);
                do content.dispose();
            }
        }

        return;
    }

    method String readCommand() {
        var String command, path;
        var Directory currentDirectory;

        let currentDirectory = state.getCurrentDirectory();
        let path = currentDirectory.getPath();
        do Output.printString(path);
        do path.dispose();

        let command = Keyboard.readLine(commandReadMessage);

        return command;
    }

    method ContentBuffer processCommand(String line) {
        var ParsedCommand command;

        let command = CommandParser.parse(line);

        if (LsProcesser.isLs(command)) {
            return LsProcesser.process(state);
        }
        if (MkdirProcesser.isMkdir(command)) {
            return MkdirProcesser.process(state, command.argumentAt(0));
        }
        if (CdProcesser.isCd(command)) {
            return CdProcesser.process(state, command.argumentAt(0));
        }
        if (RmProcesser.isRm(command)) {
            return RmProcesser.process(state, command.argumentAt(0));
        }
        if (TouchProcesser.isTouch(command)) {
            return TouchProcesser.process(state, command.argumentAt(0));
        }
        if (ClearProcesser.isClear(command)) {
            return ClearProcesser.process();
        }
        if (CatProcesser.isCat(command)) {
            return CatProcesser.process(state, command.argumentAt(0));
        }
        if (EchoProcesser.isEcho(command)) {
            return EchoProcesser.process(command.argumentAt(0));
        }

        return null;
    }

    method void printContent(ContentBuffer content) {
        var String line;
        var int i;

        let i = 0;
        while (i < content.getSize()) {
            let line = content.lineAt(i);
            do Output.printString(line);
            do Output.println();
            let i = i + 1;
        }

        return;
    }
}