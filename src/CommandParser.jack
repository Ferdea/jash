class CommandParser {
    function ParsedCommand parse(String line) {
        var String commandName;
        var StringBuilder builder;
        var List args;
        var int i;
        var char currentChar;

        let builder = StringBuilder.new(4);
        let args = List.new(4);

        let i = 0;
        while (i < line.length()) {
            let currentChar = line.charAt(i);
            if (currentChar = 32) {
                if (commandName = null) {
                    let commandName = builder.build();
                } else {
                    do args.add(builder.build());
                }
                do builder.clear();
            } else {
                do builder.add(currentChar);
            }
            let i = i + 1;
        }

        if (commandName = null) {
            let commandName = builder.build();
        } else {
            do args.add(builder.build());
        }

        do builder.dispose();
        return ParsedCommand.new(commandName, args);
    }
}