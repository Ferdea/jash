class CommandParser {
    function ParsedCommand parse(String line) {
        var String commandName, buffer;
        var Array args;
        var int i, spacesCount, argsCount;
        var char currentChar;

        let i = 0;
        let spacesCount = 0;
        while (i < line.length()) {
            if (line.charAt(i) = 32) {
                let spacesCount = spacesCount + 1;
            }
            let i = i + 1;
        }

        let buffer = String.new(16);
        if (spacesCount > 0) {
            let args = Array.new(spacesCount);
        }

        let i = 0;
        let argsCount = 0;
        while (i < line.length()) {
            let currentChar = line.charAt(i);
            if (currentChar = 32) {
                if (commandName = null) {
                    let commandName = buffer;
                } else {
                    let args[argsCount] = buffer;
                    let argsCount = argsCount + 1;
                }
                let buffer = String.new(16);
            } else {
                let buffer = buffer.appendChar(currentChar);
            }
            let i = i + 1;
        }

        if (commandName = null) {
            let commandName = buffer;
        } else {
            let args[argsCount] = buffer;
            let argsCount = argsCount + 1;
        }

        return ParsedCommand.new(commandName, argsCount, args);
    }
}