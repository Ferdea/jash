class File {
    field String name;
    field List content;
    field Directory parent;

    constructor File new(String nameArg, Directory parentArg) {
        if (nameArg.length() = 0) {
            do Sys.error(31);
        }

        let name = nameArg;
        let content = List.new(4);
        let parent = parentArg;
        return this;
    }

    method void dispose() {
        var String line;

        while (content.getSize() > 0) {
            let line = content.remove();
            do line.dispose();
        }

        do content.dispose();
        do name.dispose();
        do Memory.deAlloc(this);
        return;
    }

    method String getName() {
        return name;
    }

    method int getContentSize() {
        return content.getSize();
    }

    method String lineAt(int i) {
        return content.elementAt(i);
    }

    method void storeFromBuffer(ContentBuffer buffer) {
        var int i;
        let i = 0;
        while (i < buffer.getSize()) {
            do store(buffer.lineAt(i));
            let i = i + 1;
        }
        return;
    }

    method void store(String line) {
        do content.add(StringCopy.copy(line));
        return;
    }
}